TITLE:          
M1: Effect of daytime smartphone use (video) on sleep quality

DATA:           
FILE = 'video_99th.dat';

VARIABLE:       
NAMES = id day daytime prebed postbed sleep;    
USEVARIABLES =  daytime sleep;
LAGGED = sleep(1); !Create a lag-1 variable for sleep quality
CLUSTER = id; !The Between-Level ID variable; 
TINTERVAL = day(1); !Rescale time into one-day increments;
MISSING = ALL(-999);

ANALYSIS:       
TYPE = twolevel random; !Two-Level Model with paths that are latent variables
ESTIMATOR = BAYES; !Lagged variables can only be estimated with Bayes in Mplus
BITERATIONS = (5000); !Run at least 5,000 iterations of the MCMC algorithm
PROCESSORS = 4; !Use 4 processors to increase the computational speed
THIN = 2; !Set the thinning factor to 2 to increase stable estimations

MODEL:          
%within%                   
    phi | sleep ON sleep&1;
    dayEffect | sleep ON daytime;
    sleep;

%between%                   
    [sleep]; !mean intercept sleep quality
    [phi]; !mean of autoregressive slope
    [dayEffect]; !mean of time-varying covariate (daytime use) slope
    [daytime]; !the overall mean of daytime use
     
    sleep; !intercept variance sleep quality
    phi; !autoregressive slope variance
    dayEffect; !time-varying covariate (daytime use) slope variance
    daytime; !between-person variance of daytime use

    sleep WITH daytime; !Allow the random effects to covary; !To test the between-person association
    sleep WITH phi dayEffect; !Allow the random effect to covary
    phi WITH dayEffect daytime; 
    dayEffect WITH daytime;               

OUTPUT:         
standardized(cluster) tech1 tech8 tech4(cluster);

PLOT:
TYPE = plot1 plot2 plot3; !To request the density and trace plots for the parameter estimates
