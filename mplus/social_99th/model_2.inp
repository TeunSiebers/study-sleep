TITLE:          
M2: Effect of pre-bedtime smartphone use (social) on sleep quality

DATA:           
FILE = 'social_99th.dat';

VARIABLE:       
NAMES = id day daytime prebed postbed sleep;    
USEVARIABLES =  prebed sleep;
LAGGED = sleep(1); !Create a lag-1 variable for sleep quality
CLUSTER = id; !The Between-Level ID variable; 
TINTERVAL = day(1); !Rescale time into one-day increments;
MISSING = ALL(-999);

ANALYSIS:       
TYPE = twolevel random; !Two-Level Model with paths that are latent variables
ESTIMATOR = BAYES; !Lagged variables can only be estimated with Bayes in Mplus
BITERATIONS = (5000); !Run at least 5,000 iterations of the MCMC algorithm
PROCESSORS = 4; !Use 4 processors to increase the computational speed
THIN = 2; !Set the thinning factor to 2 to increase stable estimations

MODEL:          
%within%                   
    phi | sleep ON sleep&1;
    preEffect | sleep ON prebed;
    sleep;

%between%                   
    [sleep]; !mean intercept sleep quality
    [phi]; !mean of autoregressive slope
    [preEffect]; !mean of time-varying covariate (pre-bedtime use) slope
    [prebed]; !the overall mean of pre-bedtime use
     
    sleep; !intercept variance sleep quality
    phi; !autoregressive slope variance
    preEffect; !time-varying covariate (pre-bedtime use) slope variance
    prebed; !between-person variance of pre-bedtime use

    sleep WITH prebed; !Allow the random effects to covary; !To test the between-person association
    sleep WITH phi preEffect; !Allow the random effect to covary
    phi WITH preEffect prebed; 
    preEffect WITH prebed;               

OUTPUT:         
standardized(cluster) tech1 tech8 tech4(cluster);

PLOT:
TYPE = plot1 plot2 plot3; !To request the density and trace plots for the parameter estimates
